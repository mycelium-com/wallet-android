buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.batcha.gradle.plugins:git-dependencies:0.2'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'application'
}

mainClassName='com.mrd.bitlib.BackupUtil'
project.ext.set("mainClassName", mainClassName)

repositories {
    mavenCentral()
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

dependencies {
    compile project(includePrefix + ':bitlib')
    compile guavaArtifact
    testCompile "junit:junit:$junitVersion"
}

jar {
    manifest {
        attributes(
                "Main-Class": project.mainClassName,
                "Implementation-Version": version ?: 'dev'
        )
    }
}

static def getLatestGitHash() {
    def process = "git rev-parse --short HEAD".execute()
    return process.text.toString().trim()
}

shadowJar {
    mergeServiceFiles()
    manifest {
        attributes 'Timestamp': String.valueOf(System.currentTimeMillis())
        attributes 'Implementation-Version': getLatestGitHash()
        attributes 'Main-Class': project.mainClassName
    }
}
