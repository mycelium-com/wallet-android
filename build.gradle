apply from: 'tools.gradle'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply from: 'integration-test.gradle'

repositories {
    mavenCentral()
    google()
}

buildscript {
    apply from: 'ext_settings.gradle'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.3.2'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.10.0"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
    }
}

kapt {
    useBuildCache = true
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation,unchecked"
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    repositories {
        mavenCentral()
        google()
        maven { url 'https://jitpack.io' }
    }
}

def props = new Properties()
try {
    //this allows having a drive with the keys to be mounted for releases, only.
    props.load(new FileInputStream("keys.properties"))
} catch(Exception ignore) {
}
props.each {
    key, val -> extensions."$key" = val
}